# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>

_pyname=manila
pkgbase=openstack-$_pyname
pkgname=(openstack-$_pyname)
pkgver=16.0.0
pkgrel=1
pkgdesc="Shared Storage for OpenStack"
arch=(any)
url="https://docs.openstack.org/manila/latest/"
license=(Apache)
depends=(
	libffi
	openssl
	glibc
	mariadb
	postgresql
	python-pbr
	python-alembic
	python-eventlet
	python-greenlet
	python-lxml
	python-netaddr
	python-oslo-config
	python-oslo-context
	python-oslo-db
	python-oslo-i18n
	python-oslo-log
	python-oslo-messaging
	python-oslo-middleware
	python-oslo-policy
	python-oslo-privsep
	python-oslo-reports
	python-oslo-rootwrap
	python-oslo-serialization
	python-oslo-service
	python-oslo-upgradecheck
	python-oslo-utils
	python-oslo-concurrency
	python-osprofiler
	python-paramiko
	python-paste
	python-pastedeploy
	python-pyparsing
	python-neutronclient
	python-keystoneauth1
	python-keystonemiddleware
	python-requests
	python-tenacity
	python-routes
	python-sqlalchemy
	python-stevedore
	python-tooz
	python-cinderclient
	python-novaclient
	python-glanceclient
	python-webob
)
makedepends=(
	gettext
	graphviz
)
checkdepends=(
	base-devel
	gcc
	libxml2
	libxslt
	python-hacking
	python-bashate
	python-coverage
	python-ddt
	python-fixtures
	python-iso8601
	python-oslotest
	python-pymysql
	python-psycopg2
	python-requests-mock
	python-os-api-ref
	python-stestr
	python-testresources
	python-testscenarios
	python-testtools
)

source=(
	"https://pypi.io/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz"
	sudoers.conf
	sysusers.conf
	tmpfiles.conf
	wsgi-manila-api.conf
	openstack-manilla-scheduler.service
	openstack-manilla-share.service
)
md5sums=('26cf419a294ee321d3124c95155b3bad'
         '3b4523975186a30e9e9396c52e4beb06'
         'c4c588065936fef913c49aa1c5939d25'
         'fe004f477f313317b041593b64326232'
         '304a36191d04326a05a6009a5159cc60'
         '5b6170f3eae8770d39f44328ab51034d'
         'd11de1a1f9c48c4f1ffcb507cb88711f')
sha256sums=('2414228b48f49aa2a0a238fd6cd43f21850f6bbe52cecf52e5d7157700dbf7fe'
            'd68ee26294fae5a5d8c5bf127e9203ae683e32b03e5de778f84dd789332241fb'
            '93362645f7d108a500de091a67017194acdd8096ece9ebfbf5eabdfe3372d7d2'
            '04a95f54caf8a414122917e88fe0283350e0b92f3b7ccc8df8b1de71f2848697'
            '4fb3d5b5e5b3ea5e2592bd9701e15791b3358d4fbc0193585d67a3b6eb940963'
            '2c6f09caf213e7b383ac2fff2dd3d75352ca8a3547bed3a0acc77a376c995687'
            '266a1fc45295e87f2d9d3d0ad412e16718dc12765d1727f1cd472abec7af5f5f')
sha512sums=('ff4778b110d67ff33d14edadc4e18a454204fe542161d3e7430789891da4ea2e4d1bec4737768976ae1f34795081214f6623b6cab5455c0536ffa2eb5b78d5af'
            '05380b4e29fd5617d8cd7c909b51fae1d541ff81d2053cddf68ccc998e42acb66b84773b2d72e8476897d6155831c8a3823b4965e8a73779f96e93554ebe687a'
            '2a5922815b2e66e0ccb26130f457473c8131bef59259979f6e6d8e21f1f06b42252c135235b827fe93ce32a7faac3924e82be85521436a3ea8254dcd4ddb1eec'
            '6fd2efeaf4ae0bfdaadc58712ec41a163fa77cca988b03c6c32fa826941cec64babbe4172de6fb77a3ae6310778d5d754de1b06516cca9babe3cb87baac3f78d'
            '7b57aa4cbde6aed3ac17971997ace446481210f3588a2c5498791b5ea325040f547fc2eb1684579d280c7aae7156b6bb90690b936b35e726fa1efc10e9daf8b3'
            '4bebbacbe9afe985b2859190e135917d2e43bc1cf6be1643c9389b8049415543dd8a35853f1cd155031835b6ec80c7841b630c06ce13a16383564f15f05dc486'
            '46f5e13f130409b8267c0268659082eff1c7ad6b1d8a464bcd0f80c9da22fdb4a02aecaa1524a9bc325b0fe25c2b856f202d2492315666474cbf6260fe74d11d')

export PBR_VERSION=$pkgver

prepare(){
	cd $_pyname-$pkgver
	# Test broken with AttributeError: module 'oslo_db.sqlalchemy.test_migrations' has no attribute 'WalkVersionsMixin'
	rm manila/tests/db/migrations/alembic/test_migration.py
}

build(){
	cd $_pyname-$pkgver
	export PYTHONPATH="${PWD}"
	python setup.py build
	sphinx-build -b html doc/source doc/build/html
}

check(){
	cd $_pyname-$pkgver
	export PYTHONPATH="${PWD}"
	stestr run -E manila.tests.share.drivers.dell_emc.test_driver.EMCShareFrameworkTestCase.test_unmanage_manage
}

_package_pkg(){
	backup=(
		etc/httpd/conf/extra/wsgi-manila-api.conf
		etc/manila/api-paste.ini
		etc/manila/manila.conf
		etc/manila/policy.yaml
		etc/manila/rootwrap.conf
		etc/manila/rootwrap.d/share.filters
	)
	cd "$_pyname-$pkgver"
	export PYTHONPATH=${PWD}
	_DATADIR="$pkgdir/usr/share"
	_CONFDIR="$pkgdir/etc"
	_DOCDIR="$_DATADIR/doc"
	DATADIR="$_DATADIR/$_pyname"
	DOCDIR="$_DOCDIR/$_pyname"
	LIBDIR="$pkgdir/usr/lib"
	CONFDIR="$_CONFDIR/$_pyname"
	python setup.py install --root "$pkgdir" --optimize=1
	mv "$pkgdir"{/usr,}/etc
	for i in "${source[@]}"
	do case "${i}" in
		?*.service)install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
	esac
	done
	install -vDm644 "$srcdir/sudoers.conf" "$pkgdir/etc/sudoers.d/$_pyname"
	install -vDm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$_pyname.conf"
	install -vDm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$_pyname.conf"
	install -Dm644 "$srcdir/wsgi-manila-api.conf" -t "$pkgdir/etc/httpd/conf/extra"
	install -Dm644 "$srcdir/wsgi-manila-api.conf" -t "$DATADIR"
	oslo-config-generator --config-file=etc/oslo-config-generator/manila.conf
	oslopolicy-sample-generator --config-file=etc/manila/manila-policy-generator.conf
	install -vDm644 "etc/$_pyname/rootwrap.conf" -t "$CONFDIR"
	install -vDm644 "etc/$_pyname/rootwrap.conf" -t "$DATADIR"
	install -vDm644 "etc/$_pyname/"*.*.sample -t "$CONFDIR"
	install -vDm644 "etc/$_pyname/"*.*.sample -t "$DATADIR"
	for i in "$CONFDIR/"*.*.sample
	do mv -v "$i" "${i//.sample}"
	done
	mkdir -p "$DATADIR"
	cp -r httpd "$DATADIR"
	chmod 0750 "$pkgdir/etc/sudoers.d"
	chmod 0440 "$pkgdir/etc/sudoers.d/"*
	find "$pkgdir" -name 'README*' -exec rm -f {} \;
}

_package_doc(){
	pkgdesc="${pkgdesc} Documents"
	depends=()
	cd $_pyname-$pkgver
	DOCDIR="$pkgdir/usr/share/doc"
	mkdir -p "$DOCDIR"
	cp -r doc/build/html "$DOCDIR/${pkgname}"
	rm -r "$DOCDIR/${pkgname}/.doctrees"
}

eval "package_${pkgbase}(){ _package_pkg; }"
eval "package_${pkgbase}-doc(){ _package_doc; }"
