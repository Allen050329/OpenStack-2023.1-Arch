# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>
# Contributor: BigfootACA <bigfoot@classfun.cn>

_pyname=qpid-proton
_pycname=python-$_pyname
pkgbase=$_pycname
pkgname=(python-$_pyname)
pkgver=0.38.0
pkgrel=1
pkgdesc="An AMQP based messaging library."
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="http://qpid.apache.org/proton/"
license=(Apache)
makedepends=(
	swig
	libsasl
	openssl
	python-setuptools
)
source=("https://pypi.io/packages/source/${_pycname::1}/${_pycname}/$_pycname-$pkgver.tar.gz")
md5sums=('929d9309693cb1946870ea1a6bf55620')
sha256sums=('e39c6a19ceac0e721b7e2b29543b241c43378caf666b2911271611e88cabee4b')
sha512sums=('b9b9aacf9c43577ae8d22c616295bca155227d8af07dd5cfba0898615c5ff32d7fce62de8cd96c60ecc1290baa5334098ca1d2569d56b3671478bdece2d36b6d')

build(){
	pushd $_pycname-$pkgver
	python setup.py build
	popd
}

_package_python(){
	depends=(
		libsasl
		openssl
		python
	)
	cd $_pycname-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1
	install -Dm644 docs/* -t "$pkgdir"/usr/share/docs/$pkgname
}

eval "package_python-${_pyname}(){ _package_python; }"
