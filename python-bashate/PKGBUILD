# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>
# Contributor: BigfootACA <bigfoot@classfun.cn>

_pyname=bashate
pkgname=python-$_pyname
pkgver=2.1.1
pkgrel=1
pkgdesc="A pep8 equivalent for bash scripts"
arch=(any)
url="http://docs.openstack.org/developer/bashate/"
license=(Apache)
depends=(
	python
	python-pbr
)
makedepends=(
	python
	python-fixtures
	python-sphinx
	python-openstackdocstheme
	python-reno
	python-setuptools
)
checkdepends=(
	python-testtools
	python-stestr
	python-subunit
	python-coverage
	python-discover
	python-hacking
)
source=(https://pypi.io/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz)
md5sums=('b0de3d5aead12cfd6c42d9ce221c05ea')
sha256sums=('4bab6e977f8305a720535f8f93f1fb42c521fcbc4a6c2b3d3d7671f42f221f4c')
sha512sums=('093ad491b41b73402d316baba894b5f327f07702641f13ece4beb1578d5dfaccbff1e3e0773a799f1acd84443d5a8606288b7c586d5120bb10adcf4427d6f96c')

export PBR_VERSION=$pkgver

build(){
	cd $_pyname-$pkgver
	python setup.py build
}

check(){
	cd $_pyname-$pkgver
	stestr run
}

package(){
	cd $_pyname-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
