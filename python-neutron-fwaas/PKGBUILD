# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>

_pyname=neutron-fwaas
pkgname=python-$_pyname
pkgver=18.0.0
pkgrel=1
pkgdesc="OpenStack Networking FWaaS"
arch=(any)
url="https://docs.openstack.org/neutron-fwaas/latest/"
license=(Apache)
depends=(
	python
	python-pbr
	python-eventlet
	python-netaddr
	python-sqlalchemy
	python-alembic
	python-neutron-lib
	python-os-ken 
	python-oslo-config
	python-oslo-db
	python-oslo-log
	python-oslo-messaging
	python-oslo-service
	python-oslo-utils
	python-oslo-privsep
	python-pyroute2
	openstack-neutron
	python-pyzmq
)
makedepends=(python-setuptools)
checkdepends=(
	python-hacking
	python-coverage
	python-mock
	python-subunit
	python-requests-mock
	python-oslo-concurrency
	python-stestr
	python-testresources
	python-testtools
	python-testscenarios
	python-webob
	python-webtest
	python-oslotest
	python-pymysql
	python-psycopg2
	python-ddt
	# python-doc8
	python-pygments
)
options=('!emptydirs')
source=(https://pypi.io/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz)
md5sums=('870fc8a731bdca1e237b904eaaef759e')
sha256sums=('3f13468fec81cef980cb1d528194e33fd2d3ea1441ea2139988355077445a71d')
sha512sums=('622fce7d5792ef5afe4e1d678c703100a53f41ee2d3a06c1635830840839bf19ba557791c1dd2e2c854c291e259e83f2d6b791634c4225b19805b6c0b5e73ce6')

export PBR_VERSION=$pkgver

build(){
	cd $_pyname-$pkgver
	python setup.py build
}

check(){
	cd $_pyname-$pkgver
	stestr run
}

package(){
	cd $_pyname-$pkgver
	python setup.py install --root="$pkgdir/" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
