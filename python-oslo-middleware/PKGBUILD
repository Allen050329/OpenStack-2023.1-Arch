# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>
# Contributor: BigfootACA <bigfoot@classfun.cn>

_pyname=oslo.middleware
_pycname=${_pyname/./-}
pkgname=python-${_pycname}
pkgver=5.1.1
pkgrel=1
pkgdesc="Oslo Middleware library"
arch=(any)
url="https://docs.openstack.org/oslo.middleware/latest/"
license=(Apache)
depends=(
	python
	python-pbr
	python-jinja
	python-oslo-config
	python-oslo-context
	python-oslo-i18n
	python-oslo-utils
	python-stevedore
	python-webob
	python-debtcollector
	python-statsd
	python-bcrypt
)
makedepends=(
	python-setuptools
	python-sphinx
	python-openstackdocstheme
	python-reno
)
checkdepends=(
	python-fixtures
	python-hacking
	python-oslotest
	python-testtools
	python-coverage
	python-oslo-serialization
	bandit
	python-stestr
	python-pre-commit
)
options=('!emptydirs')
source=(https://pypi.io/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz)
md5sums=('1c4e44cc2e6b8ce1e03f58cfb5bf5d07')
sha256sums=('2166e22897be81fce648a7886d0ff8dce6bfec63a7a6f7a0cd10446e51d7c591')
sha512sums=('07dbf0986b1c22e02d8300ad7f8bcb414820a4f254991ac3aa3527691e0587aeea1df90e2e6491b9768dcae0f6f3a7fe190839da907e7f3de6a651092870724b')

export PBR_VERSION=$pkgver

build(){
	cd $_pyname-$pkgver
	export PYTHONPATH="$PWD"
	python setup.py build
	sphinx-build -b text doc/source doc/build/text
}

check(){
	cd $_pyname-$pkgver
	stestr run
}

package(){
	cd $_pyname-$pkgver
	python setup.py install --root="$pkgdir/" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	mkdir -p "$pkgdir/usr/share/doc"
	cp -r doc/build/text "$pkgdir/usr/share/doc/$pkgname"
	rm -r "$pkgdir/usr/share/doc/$pkgname/.doctrees"
}
