# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>
# Contributor: BigfootACA <bigfoot@classfun.cn>

_pyname=oslo.rootwrap
_pycname=${_pyname/./-}
pkgname=python-${_pycname}
pkgver=7.0.1
pkgrel=1
pkgdesc="Oslo Rootwrap"
arch=(any)
url="https://docs.openstack.org/oslo.rootwrap/latest/"
license=(Apache)
depends=(
	python
	python-pbr
)
makedepends=(
	python-setuptools
	python-openstackdocstheme
	python-sphinx
	python-reno
	python-mock
	python-fixtures
)
checkdepends=(
	python-hacking
	python-fixtures
	python-testtools
	python-stestr
	python-oslotest
	python-eventlet
	python-reno
	python-pre-commit
	bandit
)
options=('!emptydirs')
source=(
	https://pypi.io/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz
	0000-lost-binary2string.patch
)
md5sums=('61cdf3783ef3d46c39d6fbc2bfb7fe25'
         'ca8ec857ae77ffaa37f72ae5ffe13ec2')
sha256sums=('951aa4a9d58a3bff79e8c70a49601f74dec4e03098ad03e2c003e7ce967082ca'
            '67b36fdbb5bd83062a2f1ec0c9b7f168b4f9a082fe4c845d91dc977580897cdd')
sha512sums=('94fc3aacdeee9c846ae6b8a3e5dab8ab2b7ddffe7581578e8cee58e2e6d058a12a840525ace8473b3ef507da532e0b0b86828b84d03c1ef3c1672fd68298b316'
            'dbce2e79568b8ff36638c50d15c9cdadcf834c123f95938c5f231295e121323258e9a0ec215c6e74a7a6518685acc5466c5620c92278174235913628d1d7c0af')

prepare(){
	for i in "${source[@]}"
	do case "${i}" in
		?*.patch)
			msg2 "Apply patch ${i}"
			patch -Np1 <"${i}"
		;;
	esac
	done
}

# Documentation build is broken on python-sphinx 7.0.1-2
build(){
	cd $_pyname-$pkgver
	export PYTHONPATH="$PWD"
	python setup.py build
	# sphinx-build -b text doc/source doc/build/text
}

check(){
	cd $_pyname-$pkgver
	stestr run
}

package(){
	cd $_pyname-$pkgver
	python setup.py install --root="$pkgdir/" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -Dm644 etc/*.sample -t "$pkgdir"/usr/share/$pkgname/conf
	install -Dm644 etc/*.sample -t "$pkgdir"/etc/oslo
	for i in "$pkgdir"/etc/oslo/*.sample
	do mv -v $i ${i//.sample}
	done
	# mkdir -p "$pkgdir/usr/share/doc"
	# cp -r doc/build/text "$pkgdir/usr/share/doc/$pkgname"
	# rm -r "$pkgdir/usr/share/doc/$pkgname/.doctrees"
}
