# Maintainer: BigfootACA <bigfoot@classfun.cn>

_pyname=zvmcloudconnector
_pycname=zVMCloudConnector
pkgname=python-$_pyname
pkgver=1.6.3
pkgrel=1
pkgdesc="z/VM cloud management library in Python"
arch=(any)
url="https://github.com/mfcloud/python-zvm-sdk"
license=(Apache)
depends=(
	python
	python-jsonschema
	python-netaddr
	python-pyjwt
	python-requests
	python-routes
	python-six
	python-webob
)
makedepends=(python-setuptools)
checkdepends=(
	python-mock
	python-subunit
	python-pyaml
	python-pytest
	python-sphinx
)
source=(https://pypi.io/packages/source/${_pycname::1}/$_pycname/$_pycname-$pkgver.tar.gz)
md5sums=('c5498f4a30a9d1e3b05cef3b3e409c42')
sha256sums=('4b15ce40ad4efc7c7b26ad4c5ff139c3df0456bfc053b5095f08b07b341a8455')
sha512sums=('f9461319d92aecde886bd843563ff50ca3d0f6bd2425d266569657b2e7a2675ec596d54ba5044f93003600a83d4ab71f57ff882459c223e390556b0fe1bd2fcd')

build(){
	cd $_pycname-$pkgver
	python setup.py build
}

check(){
	cd $_pycname-$pkgver
	python -m pytest
}

package(){
	cd $_pycname-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	mv $pkgdir{,/usr}/lib/systemd
	rmdir $pkgdir/lib
	chmod 0750 $pkgdir/etc/sudoers.d
	chmod 0440 $pkgdir/etc/sudoers.d/*
}
