# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>
# Contributor: BigfootACA <bigfoot@classfun.cn>

_pyname=nova
pkgbase=openstack-$_pyname
pkgname=(openstack-$_pyname)
pkgver=27.1.0
pkgrel=1
pkgdesc="Cloud computing fabric controller"
arch=(any)
url="https://docs.openstack.org/nova/latest/"
license=(Apache)
depends=(
	libffi
	libxml2
	libxslt
	openssh
	openssl
	pcre
	lsscsi
	libvirt
	libvirt-python
	qemu-headless
	novnc
	python
	python-pbr
	python-sqlalchemy
	python-decorator
	python-eventlet
	python-jinja
	python-keystonemiddleware
	python-lxml
	python-routes
	python-cryptography
	python-webob
	python-greenlet
	python-pastedeploy
	python-paste
	python-prettytable
	python-sqlalchemy-migrate
	python-netaddr
	python-netifaces
	python-paramiko
	python-iso8601
	python-jsonschema
	python-cinderclient
	python-keystoneauth1
	python-neutronclient
	python-glanceclient
	python-requests
	python-six
	python-stevedore
	websockify
	python-oslo-cache
	python-oslo-concurrency
	python-oslo-config
	python-oslo-context
	python-oslo-log
	python-oslo-reports
	python-oslo-serialization
	python-oslo-upgradecheck
	python-oslo-utils
	python-oslo-db
	python-oslo-rootwrap
	python-oslo-messaging
	python-oslo-policy
	python-oslo-privsep
	python-oslo-i18n
	python-oslo-service
	python-rfc3986
	python-oslo-middleware
	python-psutil
	python-oslo-versionedobjects
	python-os-brick
	python-os-resource-classes
	python-os-traits
	python-os-vif
	python-os-win
	python-castellan
	python-microversion-parse
	python-os-xenapi
	python-tooz
	python-cursive
	python-pypowervm
	python-retrying
	python-os-service-types
	python-taskflow
	python-dateutil
	python-zvmcloudconnector
	python-futurist
	python-openstacksdk
	python-pyaml
	python-pymysql
)
makedepends=(
	python-setuptools
	python-sphinx
	python-sphinxcontrib-actdiag
	python-sphinxcontrib-seqdiag
	python-sphinxcontrib-svg2pdfconverter
	python-sphinx-feature-classification
	python-os-api-ref
	python-openstackdocstheme
	python-reno
	python-whereto
	python-osprofiler
)
checkdepends=(
	mypy
	python-hacking
	python-coverage
	python-ddt
	python-fixtures
	python-mock
	python-psycopg2
	python-barbicanclient
	python-ironicclient
	python-requests-mock
	python-oslotest
	python-stestr
	python-osprofiler
	python-testresources
	python-testscenarios
	python-testtools
	bandit
	python-gabbi
	python-wsgi-intercept
	python-oslo-vmware
)
conflicts=(python-nova)
source=(
	"https://pypi.io/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz"
	openstack-nova-api.service
	openstack-nova-compute.service
	openstack-nova-conductor.service
	openstack-nova-metadata-api.service
	openstack-nova-novncproxy.service
	openstack-nova-os-compute-api.service
	openstack-nova-scheduler.service
	sudoers.conf
	sysusers.conf
	tmpfiles.conf
	failure.txt
)
md5sums=('78fc13e640c0b451ea1f86ecbe19018b'
         '3f26a8660462ae32a683ad79ef733b79'
         'a3da30f3fd1b27fb60d93c4f5f988a84'
         '31b2823598ca938eb684a531be338833'
         'febf170acc9605cacfc71a38c4b22825'
         '9dbc29e1a098cce899a80db4c70135af'
         '4c111886b0d64b5983d5f03b49e4dd9f'
         'cf08895d368f0a34d65b466ad345e2a0'
         '063c88893366f685c380fae0aa678b15'
         'fb7a867fb09a081f2b56930eaa0dbe34'
         'fd68bd7a337f59eb8aff1c25fa1b3ae5'
         'c9cb12c6709018e424364dc67768baf1')
sha256sums=('ef1063d7507c7821a70f9c7a07151fac41b5be424e94bca17bb690cfa50b08a7'
            'ccee044c78f73566662b46ae1d2837b34fa998e607a5b965dff85c5042eb21de'
            '14d4425c42c7debbbf7ac0051a8af8261e84c628ef937ed0149a0434f684c6b9'
            '84803394e14ea72ea9264643052257e009e95da9f6e1bcefca89cb848e527860'
            'ef97d04c437367066261934babe31629240ec0b66929a2e32091e9fd0e13c0e7'
            'd23df8531a2d395797a21c6f1a523b0d707ae45a098ab1500025c3ed7eb23b17'
            'b4634822f93365a3a3b4133fe51698e307383936d8270a9e07a0e5cfb433db05'
            '5fd1ac31b626282c618ac2c0d2665d10e345fef2a6cb7dc45c185a75bd3a7c86'
            'a8101096ca72a2f70d003c54b3339d16697315380795d657134cf9c02388a49f'
            '00af8706d758f656f26f3845207304b9ec147211f060dceb5363feef9fdef14e'
            '20acaa2a53c2b98aed92a64280b2c1ab69a92ecc71676197c62e77549fb7efdd'
            'f3536ca009e1379de15b06ab7fe0ee7390c9151a6f0a16a6790121c2f8f68a28')
sha512sums=('f3429601793e44d2fd8dd5384f92f3e45084e4f30b0230f71a548534171533c0b239bc224f0421d451db4ad419707062642432feded3401e6f95d5093b27dd17'
            'bcd6c94e9d882528b4883fa947822e6dbea6ca8b438815556471f54f8f5bd8b413f0906d4cc210a85891fef09846972689d7d4c25ed1ae8a582fd413c22c4820'
            '2d2d701e7fa4064cf7fb9bb197453865774601228dfe8662b5c738bc17168cbfc82f1a743d024c9d5e0f5707aae6da1c7bf87a162c7031443c758f8c6db04e53'
            'f910e7ec567ddb13ff87d93ef4af0535aca21c9c667588fce26f6e7ab3e0d87093a086492a1441e031ce5b170b3c8fe6ed71509538e16b87f0ea19be1235307d'
            '6d7d0b51b138304eb0809c79be88e8bdc6890cd24c16ef5f88fe7620b4cdf9bf7143407964cdca94536a17714a0f605aa12557243b02b7fbea104592107bc053'
            '093d8db5544b7baf1522098d73ffa55f36bd3a75c49db5643027bcea999aad32b9b97840bf979fd42f5fa2609a0e5b0a5188bf3c43f8298fcca88205d461dc3b'
            '46faac9c58576a6c658a177ab0d567aa8945ce0ca170ac639f4565dc23c694b93e532da51d7a220a5fa964d6334af008b0bb1c5fd49dc2a6cd74d0e17ed8c46d'
            'a56c5e07f8c70cd87bb1ba7a16cdf2641d42d05afc31e0bad74143b8df7ee21d93b440aca33446b439e678b04bdcc23a7f010c460936a7f0faaff9c297a8f43a'
            'b2351829821824724106663a64e1237c01cec99ea5c503e92611fcceb32a1c10e85c59fee020a75e05748a8a82d89e08edaeb8f62a52843673dd59e5cfb4f6c4'
            'd2df759627d2502dc0a47a139d567e337b03ea4250396d0f8bd1ef2ca187b76fa4d9dffda50c366081d99abc55a29216a181aa11d9fc784f157a481aad04e286'
            'b02be563f4c547e27b3bbe737a203c2a326e901b2abc06c90ba8883653328479f0c3091d00a2cdf0b992de053bb0dca9fa390ce7b3b2f42f96b4bcfbfd631ae2'
            'e885f1632d602cc00f8ff693c7ab3a2c83ff941e3b08e6bb48e1a9e77dca2ca99b4c0a798117e6914209cd0c38c55905213be7728f6f1d9737e83a5855fb61c5')

export PBR_VERSION=$pkgver

prepare(){
	for i in "${source[@]}"
	do case "${i}" in
		?*.patch)
			msg2 "Apply patch ${i}"
			patch -Np1 <"${i}"
		;;
	esac
	done
}

# Documentation build is broken on python-sphinx 7.0.1-2
build(){
	cd $_pyname-$pkgver
	export PYTHONPATH="${PWD}"
	python setup.py build
	# sphinx-build -b html doc/source doc/build/html
}

check(){
	cd $_pyname-$pkgver
	export PYTHONPATH="${PWD}"
	## skip test_update_forced_down_invalid_service because https://github.com/harlowja/fasteners/issues/36
	stestr run -e $PWD/../failure.txt
}

_package_pkg(){
	optdepends=(
		"python-openstackclient: OpenStack CLI Client"
		"${pkgbase}-doc: Documents for ${pkgdesc}"
	)
	backup=(
		etc/nova/api-paste.ini
		etc/nova/logging_sample.conf
		etc/nova/nova.conf
		etc/nova/policy.yaml
		etc/nova/rootwrap.conf
		etc/nova/rootwrap.d/compute.filters
	)
	cd $_pyname-$pkgver
	export PYTHONPATH=${PWD}
	_DATADIR="$pkgdir/usr/share"
	_CONFDIR="$pkgdir/etc"
	_DOCDIR="$_DATADIR/doc"
	DATADIR="$_DATADIR/$_pyname"
	DOCDIR="$_DOCDIR/$_pyname"
	LIBDIR="$pkgdir/usr/lib"
	CONFDIR="$_CONFDIR/$_pyname"
	python setup.py install --root "$pkgdir" --optimize=1
	mv "$pkgdir"{/usr,}/etc
	for i in "${source[@]}"
	do case "${i}" in
		?*.service)install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
	esac
	done
	install -vDm644 "$srcdir/sudoers.conf" "$pkgdir/etc/sudoers.d/$_pyname"
	install -vDm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$_pyname.conf"
	install -vDm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$_pyname.conf"
	oslo-config-generator --config-file=etc/nova/nova-config-generator.conf
	oslopolicy-sample-generator --config-file=etc/nova/nova-policy-generator.conf
	install -vDm644 "etc/$_pyname/rootwrap.conf" -t "$CONFDIR"
	install -vDm644 "etc/$_pyname/rootwrap.conf" -t "$DATADIR"
	install -vDm644 "etc/$_pyname/api-paste.ini" -t "$CONFDIR"
	install -vDm644 "etc/$_pyname/api-paste.ini" -t "$DATADIR"
	install -vDm644 "etc/$_pyname/logging_sample.conf" -t "$CONFDIR"
	install -vDm644 "etc/$_pyname/logging_sample.conf" -t "$DATADIR"
	install -vDm644 "etc/$_pyname/rootwrap.d/compute.filters" -t "$CONFDIR/rootwrap.d"
	install -vDm644 "etc/$_pyname/"*.*.sample -t "$CONFDIR"
	install -vDm644 "etc/$_pyname/"*.*.sample -t "$DATADIR"
	for i in "$CONFDIR/"*.*.sample
	do mv -v "$i" "${i//.sample}"
	done
	chmod 0750 "$pkgdir/etc/sudoers.d"
	chmod 0440 "$pkgdir/etc/sudoers.d/"*
}

# _package_doc(){
# 	pkgdesc="${pkgdesc} Documents"
# 	depends=()
# 	cd $_pyname-$pkgver
# 	DOCDIR="$pkgdir/usr/share/doc"
# 	mkdir -p "$DOCDIR"
# 	cp -r doc/build/html "$DOCDIR/${pkgname}"
# 	rm -r "$DOCDIR/${pkgname}/.doctrees"
# }

eval "package_${pkgbase}(){ _package_pkg; }"
# eval "package_${pkgbase}-doc(){ _package_doc; }"
